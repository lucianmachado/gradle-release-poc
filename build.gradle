buildscript {
    ext {
        springBootVersion = '2.1.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "net.researchgate.release" version "2.8.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'io.lucian'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-actuator')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

processResources {
    def version = project.version
    def revision = file(".git/refs/heads/master").text

    println("VERSION " + version)
    println("REVISION " + revision)

    filter(org.apache.tools.ant.filters.ReplaceTokens,
            tokens: [version : project.version, revision: revision])

    inputs.property "version", version
    inputs.property "revision", revision
}

release {
    failOnCommitNeeded = true
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    revertOnFail = true
    preTagCommitMessage = '[Gradle Release Plugin] - pre tag commit: '
    tagCommitMessage = '[Gradle Release Plugin] - creating tag: '
    newVersionCommitMessage = '[Gradle Release Plugin] - new version commit: '
    tagTemplate = '${version}'
    versionPropertyFile = 'gradle.properties'
    buildTasks = ['build']
    ignoredSnapshotDependencies = []
    pushReleaseVersionBranch = 'master'
    scmAdapters = [
            net.researchgate.release.GitAdapter,
    ]

    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
        commitVersionFileOnly = false
        signTag = false
    }

}